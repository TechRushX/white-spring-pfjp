title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/sangerbund.jpg
#subtitle: Setting the direction for our newspapers and radio.

= Media

The Lachaven Times runs a veritable media empire, with many customers throughout every Sordish city and region. We could use Sebastian's connections within it for various purposes...

- @media
- @commercial
- @campaign
- @influence
- @tv
- @tv_2
- @tv_3
- @tv_4
- @return: We should not do anything with media at the moment.
- @easy_discard
- @cancel_advisor_action

@commercial
title: Make our newspapers more commercialized by broadening their appeal beyond the middle class.
subtitle: This might upset ideological purists, but it may bring in more funds and perhaps push the working class away from socialist views.
on-arrival: resources += 1; commercialized_media += 1; workers_pfjp += 4*(1 - dissent); left_dissent += 10; labor_dissent += 10

Our commercialized newspapers are able to bring in more funds. However, the left and labor factions express their displeasure.

@campaign
title: The purpose of our media is to elect more Liberals.
subtitle: Our newspapers will focus on the electoral campaign.
on-arrival: campaign_media += 1; new_middle_pfjp += 4*(1 - dissent); old_middle_pfjp += 2*(1-dissent)

Our campaign media is mostly reaching the middle class who are already saturated with Reformist propaganda, but it must be doing *something*, or at least that is what we hope.

@influence
title: Increase our influence in the Lachaven Times.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: sebastian_resigned == 0
on-arrival: lachaven_times_influence += 1; resources -= 1

We have increased our influence within the news organization. Sebastian and the Moderates are pleased, and their strength within the party only grows.

@tv
title: Why not fund a TV station?
subtitle: -2 resources
choose-if: resources >= 2
unavailable-subtitle: Not enough resources.
view-if: tv == 0
on-arrival: tv += 1; resources -= 2

We have began building up a TV station. More work needs to be done.

@tv_2
title: We must finish building up our TV network.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: tv == 1
on-arrival: tv += 1; resources -= 1; new_middle_pfjp += 5*(1 - dissent); old_middle_pfjp += 3*(1 - dissent); workers_pfjp += 3*(1 - dissent); socialism -= 3*(1 - dissent); bludish_rights += 3*(1 - dissent); pro_republic += 3*(1-dissent)

Our new TV network has a mix of popular and political programming, and is proving a success.

@tv_3
title: We must keep on building up our radio network.
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: tv >= 2 and tv <= 4
on-arrival: tv += 1; new_middle_pfjp += 5*(1 - dissent); old_middle_pfjp += 4*(1 - dissent); workers_pfjp += 4*(1 - dissent); rural_pfjp += 3*(1 - dissent); unemployed_pfjp += 2*(1-dissent); socialism -= 3*(1 - dissent); bludish_rights += 3*(1 - dissent); pro_republic += 3*(1-dissent)

Our TV network has become a successful tool for spreading our message.

@tv_4
title: We should keep on supporting our TV network.
subtitle: As television becomes more common, the novelty is beginning to wear off, and it is becoming saturated in society. -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: tv >= 5
on-arrival: tv += 1; resources -= 1; new_middle_pfjp += 3*(1 - dissent); old_middle_pfjp += 2*(1 - dissent); workers_pfjp += 3*(1 - dissent); rural_pfjp += 2*(1 - dissent); unemployed_pfjp += 2*(1-dissent); socialism -= 2*(1 - dissent); bludish_rights += 2*(1 - dissent); pro_republic += 2*(1-dissent)

We are continuing to invest in our TV network. Both commercial and political channels are becoming more prevalent, making it harder to spread our message amidst the competition.
